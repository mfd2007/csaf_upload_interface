plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id "org.openapi.generator" version "7.0.1"
    id("org.springdoc.openapi-gradle-plugin") version "1.7.0"
}

group = 'test.csaf.import'
version = '0.0.2-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.19'
	implementation 'com.networknt:json-schema-validator:1.0.87'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
    generatorName.set("html")
    inputSpec.set("$rootDir/api/api.yaml")
    outputDir.set("$rootDir/api")
}

openApi {
    outputDir.set(file("$buildDir/api"))
    outputFileName.set("api-implementation.json")
    waitTimeInSeconds.set(10)
}

task generateKey(type: Exec) {
   	workingDir "./src/main/resources/"
   	commandLine '/usr/bin/keytool', '-genkeypair','-alias','csaf','-keyalg','RSA','-keysize','4096','-validity','3650','-dname','CN=localhost','-keypass','changeit','-keystore','keystore/keystore.p12','-storeType','PKCS12','-storepass','changeit' 
   	doLast {
   		println "Keypair generated!"
   	} 	
}

generateKey.onlyIf(new Spec<Task>() {
   boolean isSatisfiedBy(Task task) {
      return ! project.file('./src/main/resources/keystore/keystore.p12').exists();
   }
});
